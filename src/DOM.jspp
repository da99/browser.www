
external document;
import DA_STANDARD.STRING;
import DA_STANDARD.COMMON;
import DA_STANDARD.SPECS;

module DA_STANDARD.DOM {

  void ID_STATE_specs() {
    should_eq(1, int () { return ID_STATE.new_id(); });
    should_eq(2, int () { return ID_STATE.new_id(); });
  }

  // === ID_STATE:
  // ===   Used to generate unique ids for elements.
  class ID_STATE {

    public static int last_id = 0;

    public static int new_id() {
      ID_STATE.last_id = ID_STATE.last_id + 1;
      return ID_STATE.last_id;
    }

    public static string new_id(string prefix) {
      return trim(prefix) + (ID_STATE.new_id().toString());
    }

  } // === class ID_STATE

  void query_selector_specs() {
    THE_STAGE.reset('<div id="query_selector_id">hello</div>');
    should_eq("hello", DOC.node("#query_selector_id").html());
  }

  class DOC {
    public static Element node(string s) {
      return new Element(document.querySelector(s));
    }

    public static NodeList node_list(string s) {
      return new NodeList(document.querySelectorAll(s));
    }
  } // class DOC

  class NodeList {
    public var origin;

    public property int length () {
      return this.origin.length;
    }

    public NodeList(v) {
      this.origin = v;
    }

    public Element item(int i) {
      return new Element(this.origin[i]);
    }

    public NodeList add_class(string new_name) {
      int l = this.origin.length;
      for(int i = 0; i < l; i++) {
        this.item(i).add_class(new_name);
      }
      return this;
    }

    public NodeList remove_class(string target) {
      int l = this.origin.length;
      for(int i = 0; i < l; i++) {
        this.item(i).remove_class(target);
      }
      return this;
    }

  } // class NodeList

  class Element {

    public var origin;

    public Element(v) {
      this.origin = v || (new document.createElement("div"));
    }

    public string html() {
      return this.origin.innerHTML;
    }

    public Element add_class(string new_class) {
      this.origin.classList.add(new_class);
      return this;
    }

    public Element remove_class(string target) {
      this.origin.classList.remove(target);
      return this;
    }

    public bool has_attr(string raw_name) {
      string val = attr(trim(raw_name));
      if (is_nothing(val) || is_empty(val))
        return false;
      return true;
    }

    public string attr(string raw_name) {
      string name = trim(raw_name);
      return trim( this.origin.getAttribute(name) || "" );
    }

    public Element attr(string raw_name, string raw_val) {
      string name = trim(raw_name);
      string val  = trim(raw_val);
      this.origin.setAttribute(name, val);
      return this;
    }

    // Returns id.
    // Sets id of element if no id is set.
    //
    // .dom_id(raw_or_jquery)
    public string dom_id() {
      return this.dom_id("element_");
    } // === id

    // Returns id.
    // Sets id of element if no id is set.
    //
    // .dom_id('prefix')
    public string dom_id(string prefix) {
      string old = this.attr('id');

      if (!is_empty(old))
        return old;

      var str = ID_STATE.new_id(prefix);
      this.attr('id', str);
      return str;
    } // === id

  } // === class Element

} // === DA_STANDARD.DOM


